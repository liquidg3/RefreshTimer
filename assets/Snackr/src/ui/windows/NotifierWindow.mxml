<?xml version="1.0" encoding="utf-8"?>

<!--
	Copyright (c) 2008 Narciso Jaramillo
	All rights reserved.

	Redistribution and use in source and binary forms, with or without 
	modification, are permitted provided that the following conditions are met:

    * Redistributions of source code must retain the above copyright notice, 
      this list of conditions and the following disclaimer.
    * Redistributions in binary form must reproduce the above copyright 
      notice, this list of conditions and the following disclaimer in the 
      documentation and/or other materials provided with the distribution.
    * Neither the name of Narciso Jaramillo nor the names of other 
      contributors may be used to endorse or promote products derived from 
      this software without specific prior written permission.

	THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS" 
	AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE 
	IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE 
	DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT OWNER OR CONTRIBUTORS BE LIABLE 
	FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL 
	DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR 
	SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER 
	CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY,
	OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE 
	USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
-->

<mx:Window xmlns:mx="http://www.adobe.com/2006/mxml" layout="absolute" 
	showTitleBar="false" systemChrome="none" type="lightweight"
	title="Snackr" transparent="true" minimizable="false" maximizable="false" resizable="false" width="100%" height="100%"
	showStatusBar="false" showGripper="false" showFlexChrome="false"
	horizontalScrollPolicy="off" verticalScrollPolicy="off"
	alpha="0"
	alwaysInFront="true" creationComplete="handleCreationComplete(event)">
	<mx:Script>
		<![CDATA[
			import mx.effects.easing.Exponential;
			import ui.utils.AnimateRectProperty;
			import mx.effects.Move;
			import mx.effects.Parallel;
			import mx.events.EffectEvent;
			import mx.effects.Fade;
			
			static private const WINDOW_MARGIN: Number = 10;
			static private const WINDOW_PADDING: Number = 10;
			static private const WINDOW_MOVE_OFFSET: Number = 50;
			
			[Bindable] public var iconURL: String = null;
			[Bindable] public var message: String = null;
			
			override protected function updateDisplayList(unscaledWidth: Number, unscaledHeight: Number): void {
				super.updateDisplayList(unscaledWidth, unscaledHeight);
				graphics.clear();
				graphics.beginFill(0x000000, 0.9);
				graphics.drawRoundRect(0, 0, unscaledWidth, unscaledHeight, 10);
				graphics.endFill();
			}
			
			private function handleCreationComplete(event: Event): void {
				positionWindow();
				
				var p: Parallel = new Parallel();
				var fader: Fade = new Fade(this);
				fader.duration = 500;
				fader.alphaFrom = 0;
				fader.alphaTo = 1;
				fader.easingFunction = Exponential.easeOut;
				p.addChild(fader);
				var mover: AnimateRectProperty = new AnimateRectProperty();
				mover.target = nativeWindow;
				mover.property = "bounds";
				mover.fromValue = nativeWindow.bounds;
				mover.toValue = new Rectangle(
					nativeWindow.bounds.x,
					nativeWindow.bounds.y - WINDOW_MOVE_OFFSET,
					nativeWindow.bounds.width,
					nativeWindow.bounds.height);
				mover.duration = 500;
				mover.easingFunction = Exponential.easeOut;
				p.addChild(mover);
				p.play();
				
				var closeTimer: Timer = new Timer(5 * 1000, 1);
				closeTimer.addEventListener(TimerEvent.TIMER_COMPLETE, handleCloseTimerComplete);
				closeTimer.start();
			}
			
			private function positionWindow(): void {
				// Position ourselves on the main screen at the lower-right corner, right above where the systray would be on Windows.
				var screen: Screen = Screen.mainScreen;
				var screenBounds: Rectangle = screen.visibleBounds;
				var windowWidth: Number = messageLabel.x + messageLabel.width + WINDOW_PADDING;
				var windowHeight: Number = messageLabel.height + 2 * WINDOW_PADDING;
				nativeWindow.bounds = new Rectangle(
					screen.visibleBounds.width - WINDOW_MARGIN - windowWidth,
					screen.visibleBounds.height - WINDOW_MARGIN - windowHeight + WINDOW_MOVE_OFFSET,
					windowWidth, windowHeight);
			}
			
			private function handleCloseTimerComplete(event: TimerEvent): void {
				var p: Parallel = new Parallel();
				var fader: Fade = new Fade(this);
				fader.duration = 500;
				fader.alphaTo = 0;
				fader.easingFunction = Exponential.easeIn;
				p.addChild(fader);
				var mover: AnimateRectProperty = new AnimateRectProperty();
				mover.target = nativeWindow;
				mover.property = "bounds";
				mover.fromValue = nativeWindow.bounds;
				mover.toValue = new Rectangle(
					nativeWindow.bounds.x,
					nativeWindow.bounds.y + WINDOW_MOVE_OFFSET,
					nativeWindow.bounds.width,
					nativeWindow.bounds.height);
				mover.duration = 500;
				mover.easingFunction = Exponential.easeIn;
				p.addChild(mover);
				p.addEventListener(EffectEvent.EFFECT_END, handleCloseEffectEnd);
				p.play();				
			}
			
			private function handleCloseEffectEnd(event: EffectEvent): void {
				close();
			}
		]]>
	</mx:Script>
	<mx:Image id="iconImage" x="10" y="10" width="16" height="16" source="{iconURL}"/>
	<mx:Text id="messageLabel" x="29" y="8" width="200" htmlText="{message}"/>
</mx:Window>
